plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

def apiKeyPropertiesFile = rootProject.file("apikey.properties")
def apiKeyProperties = new Properties()
apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))

android {
    defaultConfig {
        applicationId "com.david.moviebrowser"
        compileSdk libs.versions.sdk.compile.get() as Integer
        minSdkVersion libs.versions.sdk.min.get() as Integer
        targetSdkVersion libs.versions.sdk.target.get() as Integer
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        buildConfigField("String", "API_KEY", apiKeyProperties['API_KEY'])
        buildConfigField("String", "API_URL", "\"https://api.themoviedb.org/3/\"")
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget=17
    }

    dataBinding {
        enabled = true
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }
    namespace 'com.david.moviebrowser'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation libs.kotlin.stdlib.jdk7

    // Android support library
    implementation libs.appcompat
    implementation libs.recyclerview
    implementation libs.material
    implementation libs.constraintlayout
    implementation libs.cardview

    // Android architecture components
    implementation libs.lifecycle.extensions
    implementation libs.fragment.ktx

    // Kotlin Coroutines
    implementation libs.kotlinx.coroutines.android

    // Binding collection adapter
    implementation "me.tatarka.bindingcollectionadapter2:bindingcollectionadapter:$rootProject.ext.databindingAdapterVersion"
    implementation "me.tatarka.bindingcollectionadapter2:bindingcollectionadapter-recyclerview:$rootProject.ext.databindingAdapterVersion"

    // Retrofit
    implementation libs.retrofit
    implementation libs.converter.gson

    // Logging Interceptor
    implementation libs.logging.interceptor

    // Glide - Image loading framework
    implementation libs.glide

    // Dagger 2
    implementation "com.google.dagger:dagger:$rootProject.ext.daggerVersion"
    implementation "com.google.dagger:dagger-android:$rootProject.ext.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$rootProject.ext.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.ext.daggerVersion"

    // Dependencies for local unit tests
    testImplementation libs.junit

    // Android Testing Support Libraries
    androidTestImplementation libs.ext.junit

    // Espresso UI Testing
    androidTestImplementation libs.espresso.core
}